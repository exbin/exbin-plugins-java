group = 'org.exbin.framework.plugins'
ext.libraryName = 'FlatLaf Look and Feel Plugin'
ext.libraryDescription = 'FlatLaf plugin for ExBin Framework'

apply from: '../plugins.gradle'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath("at.bxm.gradleplugins:gradle-svntools-plugin:latest.integration")
    }
}

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

repositories {
    flatDir {
        dirs "../../lib"
        dirs "lib"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation moduleDep('ExbinFrameworkJava', 'org.exbin.framework', 'exbin-framework-api')
    implementation "com.formdev:flatlaf:0.42"
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    testImplementation group : 'junit', name: 'junit', version: '4.13.2'
}

task fatJar(type: Jar) {
    archiveBaseName = 'exbin-framework-flatlaf-laf-fat'
        project.configurations.runtimeClasspath.filter { File file -> !(
            file.name.startsWith('xbup-core') ||
            file.name.startsWith('xbup-plugin') ||
            file.name.startsWith('binary_data') ||
            file.name.startsWith('exbin-framework-api')
        )}.collect {
            from (it.isDirectory() ? it : zipTree(it).matching {
                exclude '/LICENSE.txt'
            })
        }
    duplicatesStrategy = 'exclude'
    with jar
    dependsOn('jar')
}

build.dependsOn fatJar
